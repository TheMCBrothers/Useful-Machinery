plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}

version = "$minecraft_version-$mod_version"
group = mod_group_id

repositories {
    maven { url = 'https://nexus.themcbrothers.net/repository/maven-mod-dependencies/' }
    mavenLocal()
}

base {
    archivesName = archive_base
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'neoforge.logging.markers', 'REGISTRIES'
        systemProperty 'neoforge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:$neo_version"

    // TheMCBrosLib
    implementation "net.themcbrothers:themcbroslib:$lib_version"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version, neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, version: version,
            mod_authors         : mod_authors, mod_description: mod_description,
            github_repo         : github_repo, curse_project: curse_project,
            lib_version_range   : lib_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${rootProject.name}" }
    }

    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }

    exclude('net/themcbrothers/**/datagen/**')
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
            artifactId = archive_base
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv('NEXUS_UNAME')
                password = System.getenv('NEXUS_PWD')
            }

            url 'https://nexus.themcbrothers.net/repository/maven-releases/'
        }
    }
}

unifiedPublishing {
    project {
        displayName = "[$minecraft_version] v$mod_version"
        releaseType = artifact_type
        gameVersions = [minecraft_version]
        gameLoaders = ['neoforge']

        mainPublication tasks.jar

        var curseforgeToken = System.getenv('CURSEFORGE_TOKEN')
        var modrinthToken = System.getenv('MODRINTH_TOKEN')
        var changes = file('changelog.md').text

        if (curseforgeToken != null && !curse_project.isEmpty()) {
            curseforge {
                token = curseforgeToken
                id = curse_project
                changelog = changes
                gameVersions.addAll 'Java 17'
            }
        }

        if (modrinthToken != null && !modrinth_project.isEmpty()) {
            modrinth {
                token = modrinthToken
                id = modrinth_project
                changelog = changes.substring(changes.indexOf('\n'), changes.indexOf('\n\n'))
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
