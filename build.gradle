plugins {
    id "idea"
    id "maven-publish"
    id "net.minecraftforge.gradle" version "5.1.+"
    id "me.hypherionmc.cursegradle" version "2.0.1"
    id "com.modrinth.minotaur" version "2.+"
}

group = "net.themcbrothers" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "usefulmachinery"
version = "${mc_version}-${mod_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "official", version: project.mc_version

    runs {
        client {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                usefulmachinery {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            mods {
                usefulmachinery {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")

            // Recommended logging data for a userdev environment
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"

            // Recommended logging level for the console
            property "forge.logging.console.level", "debug"

            args "--mod", "usefulmachinery", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                usefulmachinery {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir "src/generated/resources"
}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Blamejared"
        url = "https://nexus.themcbrothers.net/repository/maven-blamejared"
    }
    maven {
        // TOP
        name = "k4you"
        url = "https://nexus.themcbrothers.net/repository/maven-k4you"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://nexus.themcbrothers.net/repository/maven-mod"
    }
    maven {
        url "https://nexus.themcbrothers.net/repository/maven-curse"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "themcb-nexus"
        url = "https://nexus.themcbrothers.net/repository/maven-releases/"
    }
    maven {
        name = "themcb-nexus-snapshots"
        url = "https://nexus.themcbrothers.net/repository/maven-snapshots/"
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then "net.minecraft" it is assumed
    // that the dep is a ForgeGradle "patcher" dependency. And it"s patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    if (project.hasProperty("jei_version")) {
        // JEI
        compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
        compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
        runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")
    }

    if (project.hasProperty("top_version")) {
        // TOP
        compileOnly "mcjty.theoneprobe:theoneprobe:${top_version}:api"
        runtimeOnly fg.deobf("mcjty.theoneprobe:theoneprobe:${top_version}", { transitive = false })
    }

    if (project.hasProperty("usefulfoundation_version")) {
        // Useful Foundation
        implementation fg.deobf("net.themcbrothers:useful-foundation:${usefulfoundation_version}")
    }

    if (project.hasProperty("themcbroslib_version")) {
        // TheMCBrosLib
        implementation fg.deobf("net.themcbrothers:TheMCBrosLib:${themcbroslib_version}")
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "usefulmachinery",
                "Specification-Vendor"    : "themcbrothers",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion.get(),
                "Implementation-Vendor"   : "themcbrothers",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        PublicationContainer publicationContainer ->
            publicationContainer.register("maven", MavenPublication) {
                MavenPublication publication ->
                    publication.artifacts = [jar]
                    publication.version = project.version
                    publication.artifactId = "useful-machinery"
            }
    }

    repositories {
        maven {
            credentials {
                username = getSecret("nexus_uname")
                password = getSecret("nexus_pwd")
            }

            url "https://nexus.themcbrothers.net/repository/maven-releases/"
        }
    }
}

private String getSecret(String key) {
    def secret = System.getenv(key)
    if (secret != null) {
        return secret.trim()
    }

    File secretFile = file('secrets.properties')
    if (secretFile.canRead()) {
        Properties secretProps = new Properties()
        secretProps.load(new FileInputStream(secretFile))
        return secretProps.getProperty(key).toString()
    }
    return null
}

var curseForgeKey = getSecret('cf_key')
var modrinthToken = getSecret('modrinth_token')
var refName = System.getenv('ref_name') ?: 'HEAD'

// Publish to CurseForge
if (curseForgeKey != null) {
    curseforge {
        apiKey = curseForgeKey

        project {
            id = curse_project
            changelog = file('changelog.txt')
            changelogType = "markdown"
            releaseType = artifact_type
            addGameVersion(mc_version)
            mainArtifact(tasks.jar) {
                displayName = "$modname $mod_version"
                relations {
                    requiredDependency "useful-foundation"
                    requiredDependency "themcbroslib"
                    optionalDependency "jei"
                    optionalDependency "the-one-probe"
                }
            }
        }
    }
}

// Publish to Modrinth
if (modrinthToken != null) {
    modrinth {
        token = modrinthToken
        projectId = modrinth_project
        versionName = "$modname $mod_version"
        versionType = artifact_type
        changelog = "[View changelog.txt on GitHub](https://raw.githubusercontent.com/TheMCBrothers/Useful-Machinery/$refName/changelog.txt)"
        uploadFile = tasks.jar
        dependencies {
            required.project "themcbroslib"
            required.project "useful-foundation"
            optional.project "jei"
            optional.project "the-one-probe"
        }
    }
}
